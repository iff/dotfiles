#!/bin/zsh
set -eu -o pipefail

# this script roughly acts like openssh
# at least in terms of how git uses it

if [[ $1 == '-G' && $# == 2 ]]; then
    ssh $@
    exit
fi

if [[ ! $2 =~ "git-(upload|receive)-pack '(.*)'" && $# == 2 ]]; then
    echo 'unexpected form' $@ >&2
    exit 1
fi

target=$1:$match[2]

case $target in

    (git@github.com:dkuettel/*)
        key=private
        user="Yves Ineichen iff@yvesineichen.com"
        ;;

    (git@github.com:iff/*)
        key=private
        user="Yves Ineichen iff@yvesineichen.com"
        ;;

    (git@github.com:werehamster/*)
        key=private
        user="Yves Ineichen iff@yvesineichen.com"
        ;;

    (git@github.com:ThingWorx/*)
        key=work
        user="Yves Ineichen yineichen@ptc.com"
        ;;

    (*)
        echo 'no match for' $target >&2
        exit 1
        ;;

esac

if [[ -e .git && -v user ]]; then
    if ! actual="$(git config --get user.name) $(git config --get user.email)"; then
        echo 'no git user is set instead of' $user >&2
        exit 1
    fi
    if [[ $actual != $user ]]; then
        echo 'git user is' $actual 'but expected' $user >&2
        exit 1
    fi
fi

echo $target '->' $key >&2
ssh -i ~/.ssh/$key $@
